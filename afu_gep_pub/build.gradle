import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

apply plugin: 'ch.so.agi.gretl'
apply plugin: "de.undercouch.download"

defaultTasks 'downloadData'

def pathToTempFolder = System.getProperty("java.io.tmpdir")

def dataSets = ['2471_gep']

def schema = "afu_gep_v1"

dataSets.each { dataSet ->

    def dataSetUrl = "https://s3.eu-central-1.amazonaws.com/ch.so.agi.geodata-dev/"+dataSet+".xtf"

    tasks.register("download_$dataSet", Download) {
        src dataSetUrl
        dest pathToTempFolder
        overwrite true
    }

    tasks.register("replace_$dataSet", Ili2pgReplace) {
        dependsOn "download_$dataSet"
        database = [dbUriEdit, dbUserEdit, dbPwdEdit]
        dbschema = schema
        models = 'VSADSSMINI_2020_LV95'
        dataset = dataSet
        dataFile = Paths.get(pathToTempFolder, dataSet + ".xtf").toFile()
        disableValidation = true // Weil gewissen Dinge herabgeschwächt werden in der Prüfung. Die ini-Datei ist noch in keinem Repo. Eventuell müsste man auch noch den Task anpassen, damit das überhaupt geht. Resp. geht es überhaupt mit ili2db?
    }
}

task replaceData() {
    description = "Replace datasets."
    dependsOn {
        tasks.findAll { task -> task.name.startsWith('replace_') }
    }
}

