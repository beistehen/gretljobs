import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'

defaultTasks 'importXTF_stage', 'importXTF_pub'

def FileName = "arp_naturreservate.xtf"
def FileNamePub = "arp_naturreservate_pub.xtf"

task exportDataEdit (type: Ili2pgExport) {
    description = "Exportiert die Daten aus dem Schema arp_naturreservate in ein INTERLIS-Datei."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = 'arp_naturreservate'
    models = 'SO_ARP_Naturreservate_20200609'
    if (findProperty('ili2dbModeldir')) modeldir = ili2dbModeldir
    dataFile= file(FileName)
    disableValidation = true
}

task validateDataEdit(type: IliValidator, dependsOn: 'exportDataEdit') {
    description = "Validiert die exportierten INTERLIS-Datei gegen das Modell."
    dataFiles = file(FileName)
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
    failOnError = true
}

task transferArpNaturreservate(type: Db2Db, dependsOn: 'validateDataEdit'){
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet('arp_naturreservate_pub_naturreservate_pflanzenliste.sql',
                    'arp_naturreservate_staging.naturreservate_pflanzenliste', true),
            new TransferSet('arp_naturreservate_pub_naturreservate_reservate.sql',
                    'arp_naturreservate_staging.naturreservate_reservat', true),
            new TransferSet('arp_naturreservate_pub_naturreservate_teilgebiete.sql',
                    'arp_naturreservate_staging.naturreservate_teilgebiet', true)
    ];
}

task exportData (type: Ili2pgExport, dependsOn: 'transferArpNaturreservate') {
    description = "Exportiert die umgebauten Daten aus dem Schema arp_naturreservate_staging in ein INTERLIS-Datei."
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dbschema = 'arp_naturrservate_staging_v1'
    models = 'SO_ARP_Naturreservate_Publikation_20200609'
    if (findProperty('ili2dbModeldir')) modeldir = ili2dbModeldir
    dataFile= file(FileNamePub)
    disableValidation = true
}

task validateData(type: IliValidator, dependsOn: 'exportData') {
    description = "Validiert die exportierten INTERLIS-Datei gegen das Modell."
    dataFiles = file(FileNamePub)
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
    failOnError = true
}

task importXTF_stage(type: Ili2pgReplace, dependsOn: 'validateData') {
    description = 'Import der umgebaute INTERLIS-Datei  in das Schema arp_naturreservate_staging.'
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dbschema = 'arp_natureservate_staging_v1'
    models = 'SO_ARP_Naturreservate_Publikation_20200609'
    if (findProperty('ili2dbModeldir')) modeldir = ili2dbModeldir
    dataFile = file(FileNamePub)
    disableValidation = true
}

task delete_Dataset_stage(type: Ili2pgDelete) {
    description = "LÃ¶scht Daten aus dem Schema arp_naturreservate_staging"
    database = [dbUriPub, dbUserPub, dbPwdPub]
    models = 'SO_ARP_Naturreservate_Publikation_20200609'
    if (findProperty('ili2dbModeldir')) modeldir = ili2dbModeldir
    dbschema = 'arp_naturreservate_staging_v1'
}

task refreshSolr(type:Exec, dependsOn:'transferArpNaturreservate') {
	commandLine 'curl', '-i', '--max-time', '5', solrIndexupdaterBaseUrl + '/queue?ds=ch.so.arp.naturreservate.reservate'
}

task importXTF_pub(type: Ili2pgReplace) {
    description = 'Import der umgebaute INTERLIS-Datei in das Schema arp_naturreservate_pub.'
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dbschema = 'arp_naturreservate_pub'
    models = 'SO_ARP_Naturreservate_Publikation_20200609'
    if (findProperty('ili2dbModeldir')) modeldir = ili2dbModeldir
    dataFile = file(FileNamePub)
    disableValidation = true
    finalizedBy delete_Dataset_stage, refreshSolr
}
